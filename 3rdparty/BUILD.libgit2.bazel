load("@rules_foreign_cc//foreign_cc:defs.bzl", "cmake")

filegroup(
    name = "all_srcs",
    srcs = glob(
        ["**"],
        exclude = ["tests/**"],
    ),
)

_CACHE_ENTRIES = {
    "BUILD_CLAR": "off",
    "BUILD_EXAMPLES": "off",
    "BUILD_FUZZERS": "off",
    "BUILD_SHARED_LIBS": "off",
    "REGEX_BACKEND": "builtin",
    "USE_HTTPS": "off",
    "USE_SSH": "off",
}

_LINUX_CACHE_ENTRIES = dict(_CACHE_ENTRIES.items() + {
    "CMAKE_C_FLAGS": "${CMAKE_C_FLAGS:-} -fPIC",
}.items())

cmake(
    name = "libgit2",
    cache_entries = select({
        "@platforms//os:linux": _LINUX_CACHE_ENTRIES,
        "//conditions:default": _CACHE_ENTRIES,
    }),
    lib_source = ":all_srcs",
    # TODO: Linux redhat systems use `lib64`. There should be a way to detect
    # this and do the appropriate thing.
    #
    # out_lib_dir = "lib64",
    out_static_libs = select({
        # TODO: I'm guessing at this name. Needs to be checked on windows.
        "@platforms//os:windows": ["git2.lib"],
        "//conditions:default": ["libgit2.a"],
    }),
    visibility = ["//visibility:public"],
)
